
// Вставить написать вступление и описание

// Совет для начинающих: удобнее всего читать такой код с конца




// -------------------------------------------------
// --- 5. Вспомогательные функции
// -------------------------------------------------

// --- Проверить, содержит ли одна строка другую?
function isContains(findingText: string; inSourceText: string) : boolean;
begin
	result := pos(findingText, inSourceText) <> 0;
end;


// --- Округление до нужного количества знаков после запятой
// --- (стандартные функции округления у меня почему-то не заработали)
function roundFor(sourceNumber: Double  ; digitAfterComma: integer) : string;
var
	intPart, floatPart : integer;
begin    
	intPart := floor(sourceNumber);
	floatPart := floor( (sourceNumber - intPart) * round(intPower(10, digitAfterComma)) )
	result := intToStr(intPart) + '.' + intToStr(floatPart)
end;


// --- Вычислить номер тайла в системе Quadkey (используется в картах Bing)
function getQuadkeyText(x: integer; y: integer; z: integer) : string;
var
	i, q : byte;
begin
	result:='';
	for i:=1 to z do begin
		q:=0;
		if x mod 2 = 1 then q := q + 1;
		if y mod 2 = 1 then q := q + 2;
		x := x div 2;
		y := y div 2;
		result := intToStr(q) + result;
	end;
end;




// -------------------------------------------------
// --- 4. Если требуется, то подставить имя для зеркала сервера 
// --- на место заглушки типа {s: a,b,c}
// -------------------------------------------------
type
	TSubst = record
		mask, val : string;
	end;
	TSubsts = record
		count : integer;
		s : array [0..15] of TSubst;
	end;


function replaceServerName(url: string) : string;
var
	s, ss : string;
	sarr : array [0..9] of string;
	sarr_l, p : integer;

begin
	s := RegExprGetMatchSubStr(url, '\{[sS]:([^}]+)\}', 0);
	if s <> '' then begin
		ss := s;
		ss := StringReplace(ss, '{s:', '', [rfIgnoreCase]);
		ss := StringReplace(ss, '}', '', [rfIgnoreCase]);
		ss := ss + ',';
		sarr_l := 0;
		while ss <> '' do begin
			p := pos(',', ss);
			if p = 0 then p := length(ss);
			sarr[sarr_l] := copy(ss, 1, p-1);
			sarr_l := sarr_l + 1;
			delete(ss, 1, p);
		end;
		url := StringReplace(url, s, sarr[random(sarr_l)], []);
	end;
	Result := url;
end;



// -------------------------------------------------
// --- 3. Если требуется, то вычислить и подставить в шаблон URL адреса 
// --- нужные значения на место заглушек типа {x}, {y}, {z}.
// -------------------------------------------------
function replaceLeafletPlaceholders(urlTemplate: string; x: integer; y: integer; z: integer) : string;
var
	options: tReplaceFlags;
	calculatedValue: string;

begin
	options := [rfReplaceAll, rfIgnoreCase];
	result := urlTemplate;

	if isContains('{x}', result) then begin
		calculatedValue := intToStr(x);
		result := stringReplace( result, '{x}', calculatedValue, options)
	end;

	if isContains('{y}', result) then begin
		calculatedValue := intToStr(y);
		result := stringReplace( result, '{y}', calculatedValue, options)
	end;

	if isContains('{z}', result) then begin
		calculatedValue := intToStr(z-1);
		result := stringReplace( result, '{z}', calculatedValue, options)
	end;

	if isContains('{z+1}', result) then begin
		calculatedValue := intToStr(z);
		result := stringReplace( result, '{z+1}', calculatedValue, options)
	end;

	if isContains('{x/1024}', result) then begin
		calculatedValue := intToStr(x div 1024);
		result := stringReplace( result, '{x/1024}', calculatedValue, options)
	end;

	if isContains('{y/1024}', result) then begin
		calculatedValue := intToStr(y div 1024);
		result := stringReplace( result, '{y/1024}', calculatedValue, options)
	end;

	if isContains('{-y}', result) then begin
		calculatedValue := intToStr( round(intPower(2, z-1)) - 1 - y);
		result := stringReplace( result, '{-y}', calculatedValue, options)
	end;

	if isContains('{q}', result) then begin
		calculatedValue := getQuadkeyText(x, y, z);
		result := stringReplace( result, '{q}', calculatedValue, options)
	end;

	if isContains('{bbox}', result) then begin
        result := stringReplace( result, '{bbox}', '{Left},{Bottom},{Right},{Top}', options)
        result := stringReplace( result, '{Left}', roundFor(GetLMetr,8), options)
        result := stringReplace( result, '{Bottom}', roundFor(GetBMetr,8), options)
        result := stringReplace( result, '{Right}', roundFor(GetRMetr,8), options)
        result := stringReplace( result, '{Top}', roundFor(GetTMetr,8), options)
        // Делаю замену в пять операций потому, что при попытке 
        // сделать все одной строкой возникает ошибка. Возможно какое-то ограничение на память.
	end;

	if isContains('{timeStamp}', result) then begin
		calculatedValue := IntToStr(GetUnixTime);
		result := stringReplace( result, '{timeStamp}', calculatedValue, options)
	end;
end;




// -------------------------------------------------
// --- 2. Главная функция. Просто запускает все подфункции
// -------------------------------------------------
function fillUrlTemplate(urlTemplate: string; x: integer; y: integer; z: integer) : string;
begin
	result := replaceLeafletPlaceholders(urlTemplate, x, y, z);
	result := replaceServerName(result);
end;



// -------------------------------------------------
// --- 1. Старт скрипта. Запустить вычисление URL адреса для скачивания тайла
// -------------------------------------------------
begin
	// Возвращаемое значение: готовый URL адрес для скачивания
	resultURL := fillUrlTemplate(getURLBase, getX, getY, getZ);


	// Подсказка: с помощью этой команды можно 
	// показать произвольное текстовое значение в окне Debug Output 
	// во встроенном в SasPlanet редакторе PascalScript IDE.
	// Можжете вставлять эту строку в любое место кода и 
	// использовать для отладки при написании собственных скриптов:
	//
	// version := 'Hello, SasPlanet!'; 	
end.
